        /               'B' @=@@=@@++++#
       // /             'u' @@@@@=@+++++#
      // // /           'z' @=@@@@+@++++#
     // // // /         'i' @@@@@@=+++++#
    // // // // /       'F' @@@=@@+++++#
   // // // // // /      NL  ++++++++++#
  // // // // // // /   100 @@@=@@@=++++#
$@/>@/>@/>@/>@/>@/>@/\   0
/                    / http://rosettacode.org/wiki/FizzBuzz
!                             /======= Fizz <<<.<.<..>>>#
/                             |      \
\?!#->+ @\.>?!#->+ @\.>?!#->+@/.>\   |
/        !          !         !  /   |
\?!#->+ @\.>?!#->+@\ .>?!#->+@/.>\   |
/        !         \!===\!    !  /   |
\?!#->+ @\.>?!#->+ @\.>?!#->+@/.>\   |
/        !          !   |     !  /   |
\?!#->+@\ .>?!#->+ @\.>?!#->+@/.>\   |
/       \!==========!===\!    !  /   |
\?!#->+ @\.>?!#->+ @\.>?!#->+@/>>@\.>/
         !          |   |         |
         /==========/   \========!\=== Buzz <<<<<<<.>.>..>>>#
         |
         \!/=dup==?\>>@\<!/back?\<<<#
           \<<+>+>-/   |  \>+<- /
                       |
/======================/
|
|       /recurse\    #/?\ zero
\print=!\@\>?!\@/<@\.!\-/
          |   \=/  \=itoa=@@@+@+++++#
          !     /+ !/+ !/+ !/+   \    mod10
          /<+> -\!?-\!?-\!?-\!?-\!
          \?!\-?!\-?!\-?!\-?!\-?/\    div10
             #  +/! +/! +/! +/! +/
